plugins {
    id 'java'
    id 'idea'
    id 'fabric-loom'
    id 'lol.bai.explosion'
}

base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}

fabricApi {
    configureDataGeneration {
        modId = "ali"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    implementation project(":api")
    implementation project(":common")

    // EMI
    modCompileOnlyApi "dev.emi:emi-fabric:${emi_version}:api"
    modCompileOnly "dev.emi:emi-fabric:${emi_version}"
    modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

    // JEI
    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
//    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    // REI
    modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
    modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}"
//    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"

    // LootJS
    modImplementation "curse.maven:kubejs-238086:5853321"
    modImplementation "curse.maven:rhino-416294:6186969"
    modImplementation "curse.maven:architectury-api-419699:5137936"
    modImplementation "curse.maven:lootjs-570630:5404564"

    // Aether
    modImplementation explosion.fabric("curse.maven:aether-255308:6800393")
    modImplementation explosion.fabric("curse.maven:accessories-938917:6304691")
    modImplementation explosion.fabric("curse.maven:owo-lib-532610:4749199")
    modImplementation "curse.maven:cloth-config-348521:5729104"

    // Charm Forked
    modImplementation explosion.fabric("curse.maven:charm-forked-1016482:6492694")

    // Farmer's Delight
    modImplementation explosion.fabric("curse.maven:farmers-delight-refabricated-993166:6626608")

    // Testing
//    modLocalRuntime "curse.maven:supplementaries-412082:6889002"
//    modLocalRuntime "curse.maven:selene-499980:6902843"
//    include "curse.maven:c2me-533097:6181291"
//    modLocalRuntime "curse.maven:c2me-533097:6181291"
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }

        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":api").sourceSets.main.allSource)
    source(project(":common").sourceSets.main.allSource)
    source(project(":common-rei").sourceSets.main.allSource)
    source(project(":common-lootjs").sourceSets.main.allSource)

    source(project(":compatibility-aether").sourceSets.main.allSource)
    source(project(":compatibility-charm-forked").sourceSets.main.allSource)
    source(project(":compatibility-farmers-delight").sourceSets.main.allSource)
}

tasks.named("sourcesJar", Jar) {
    from(project(":api").sourceSets.main.allSource)
    from(project(":common").sourceSets.main.allSource)
    from(project(":common-rei").sourceSets.main.allSource)
    from(project(":common-lootjs").sourceSets.main.allSource)

    from(project(":compatibility-aether").sourceSets.main.allSource)
    from(project(":compatibility-charm-forked").sourceSets.main.allSource)
    from(project(":compatibility-farmers-delight").sourceSets.main.allSource)
}

processResources {
    from project(":api").sourceSets.main.resources
    from project(":common").sourceSets.main.resources
    from project(":common-lootjs").sourceSets.main.resources

    from project(":compatibility-aether").sourceSets.main.resources
    from project(":compatibility-charm-forked").sourceSets.main.resources
    from project(":compatibility-farmers-delight").sourceSets.main.resources
}