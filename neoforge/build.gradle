plugins {
    id 'com.gradleup.shadow'
}

base {
    archivesName = "${mod_name}-neoforge"
}

architectury {
    platformSetupLoomIde()
    neoForge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    mixin {
        useLegacyMixinAp = true
        defaultRefmapName = "ali.neoforge.refmap.json"
    }

    runs {
        data {
            data()
            programArgs "--all", "--mod", "${mod_id}"
            programArgs "--output", "${file('src/main/generated/')}"
            programArgs "--existing", "${file("src/main/resources").absolutePath}"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentNeoForge.extendsFrom common
}

dependencies {
    neoForge "net.neoforged:neoforge:${neoforge_version}"

    project.ext.commonProjects.each { p ->
        common(project(path: p, configuration: "namedElements")) { transitive false }
        shadowCommon(project(path: p, configuration: "transformProductionNeoForge")) { transitive false }
    }

    // LootJS
    modCompileOnly "curse.maven:kubejs-238086:5810100"
    modCompileOnly "curse.maven:rhino-416294:6184623"
    modCompileOnly "curse.maven:lootjs-570630:6792267"

    // Aether https://www.curseforge.com/minecraft/mc-mods/aether
    modCompileOnly "curse.maven:aether-255308:6894706"

    // Deeper and Darker https://www.curseforge.com/minecraft/mc-mods/deeperdarker
    modCompileOnly "curse.maven:deeperdarker-659011:6463247"

    // Farmer's Delight https://www.curseforge.com/minecraft/mc-mods/farmers-delight
    modCompileOnly "curse.maven:farmers-delight-398521:6917256"

    // Hybrid Aquatic https://www.curseforge.com/minecraft/mc-mods/hybrid-aquatic
//    modCompileOnly fg.deobf("curse.maven:hybrid-aquatic-834427:6497306")

    // Immersive Engineering https://www.curseforge.com/minecraft/mc-mods/immersive-engineering
    modCompileOnly "curse.maven:immersive-engineering-231951:6733669"

    // Moonlight https://www.curseforge.com/minecraft/mc-mods/selene
    modCompileOnly "curse.maven:selene-499980:7007381"

    // Repurposed Structures https://www.curseforge.com/minecraft/mc-mods/repurposed-structures
    modCompileOnly "curse.maven:repurposed-structures-368293:6797352"

    // Sawmill https://www.curseforge.com/minecraft/mc-mods/sawmill
    modCompileOnly "curse.maven:sawmill-964817:6979313"

    // Snow! Real Magic! https://www.curseforge.com/minecraft/mc-mods/snow-real-magic
    modCompileOnly "curse.maven:snow-real-magic-308663:6783557"

    // Supplementaries https://www.curseforge.com/minecraft/mc-mods/supplementaries
    modCompileOnly "curse.maven:supplementaries-412082:7008519"

    // The Bumblezone https://www.curseforge.com/minecraft/mc-mods/the-bumblezone-forge
    modCompileOnly "curse.maven:the-bumblezone-forge-362479:6960939"

    // Villagers plus https://www.curseforge.com/minecraft/mc-mods/villagersplus-forge
//    modCompileOnly fg.deobf("curse.maven:villagersplus-forge-817272:4996995")

    // Mantle https://www.curseforge.com/minecraft/mc-mods/mantle
//    modCompileOnly "curse.maven:mantle-74924:6819217"
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
    archiveClassifier = null
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}