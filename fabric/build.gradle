plugins {
    id 'java'
    id 'idea'
    id 'fabric-loom'
    id 'lol.bai.explosion'
}

base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}

fabricApi {
    configureDataGeneration {
        modId = "ali"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    implementation project(":api")
    implementation project(":common")

    // EMI
    modCompileOnlyApi "dev.emi:emi-fabric:${emi_version}:api"
    modCompileOnly "dev.emi:emi-fabric:${emi_version}"
    modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

    // JEI
    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
//    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    // REI
    modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
    modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}"
//    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"

    // LootJS
    modImplementation "curse.maven:kubejs-238086:5853321"
    modImplementation "curse.maven:rhino-416294:6186969"
    modImplementation "curse.maven:architectury-api-419699:5137936"
    modImplementation "curse.maven:lootjs-570630:5404564"

    // Aether
    modCompileOnly explosion.fabric("curse.maven:aether-255308:6800393")

    // Charm Forked
    modCompileOnly explosion.fabric("curse.maven:charm-forked-1016482:6492694")

    // Farmer's Delight
    modCompileOnly explosion.fabric("curse.maven:farmers-delight-refabricated-993166:6626608")

    // Supplementaries
    modCompileOnly "curse.maven:supplementaries-412082:6889002"

    // Moonlight
    modCompileOnly "curse.maven:selene-499980:6978610"

    // Sawmill
    modCompileOnly "curse.maven:sawmill-964817:6948520"

    // The Bumblezone
    modCompileOnly "curse.maven:the-bumblezone-fabric-363949:6960934"

    // Villagers plus
    modCompileOnly "curse.maven:villagersplus-fabric-809542:4996993"

    // Cultural Delights
    modCompileOnly "curse.maven:cultural-delights-fabric-807107:5510598"

    // Hybrid Aquatic
    modCompileOnly "curse.maven:hybrid-aquatic-834427:6497302"

    // Snow! Real Magic!
    modCompileOnly "curse.maven:snow-real-magic-fabric-560441:6779484"

    // Deeper and Darker
    modCompileOnly "curse.maven:deeperdarker-659011:6007575"
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }

        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.withType(JavaCompile).configureEach {
    source(project(":api").sourceSets.main.allSource)
    source(project(":common").sourceSets.main.allSource)
    source(project(":common-rei").sourceSets.main.allSource)
    source(project(":common-lootjs").sourceSets.main.allSource)
}

tasks.named("sourcesJar", Jar) {
    from(project(":api").sourceSets.main.allSource)
    from(project(":common").sourceSets.main.allSource)
    from(project(":common-rei").sourceSets.main.allSource)
    from(project(":common-lootjs").sourceSets.main.allSource)
}

processResources {
    from project(":api").sourceSets.main.resources
    from project(":common").sourceSets.main.resources
    from project(":common-lootjs").sourceSets.main.resources
}