plugins {
    id "com.github.johnrengelman.shadow" version "7.1.2"
}

base {
    archivesName = "${mod_name}-forge"
}

architectury {
    platformSetupLoomIde()
    forge()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    forge {
        convertAccessWideners = true

        mixinConfig "ali.mixins.json"
        mixinConfig "ali.emi.mixins.json"
        mixinConfig "ali.lootjs.mixins.json"
        mixinConfig "ali.forge.mixins.json"
    }

    runs {
        data {
            data()
            programArgs "--all", "--mod", "${mod_id}"
            programArgs "--output", "${file('src/main/generated/')}"
            programArgs "--existing", "${file("src/main/resources").absolutePath}"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common
}

dependencies {
    modImplementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    def commonProjects = [
            ":api",
            ":common",
            ":common-emi",
            ":common-jei",
            ":common-rei",
            ":common-lootjs"
    ]

    commonProjects.each { p ->
        common(project(path: p, configuration: "namedElements")) { transitive false }
        shadowCommon(project(path: p, configuration: "transformProductionFabric")) { transitive false }
    }

    // EMI
//    modLocalRuntime "dev.emi:emi-forge:${emi_version}"

    // JEI
    modLocalRuntime "mezz.jei:jei-${minecraft_version}-forge:${jei_version}"

    // REI
    modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-api-forge:${rei_version}"
//    modLocalRuntime "me.shedaniel:RoughlyEnoughItems-forge:${rei_version}"
//    modLocalRuntime "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"
//    modLocalRuntime "dev.architectury:architectury-forge:${architectury_version}"

    // LootJS
    modCompileOnly "curse.maven:kubejs-238086:5853326"
    modCompileOnly "curse.maven:rhino-416294:6186971"
    modCompileOnly "curse.maven:lootjs-570630:5404565"

    // Aether https://www.curseforge.com/minecraft/mc-mods/aether
    modCompileOnly "curse.maven:aether-255308:6134920"

    // Deeper and Darker https://www.curseforge.com/minecraft/mc-mods/deeperdarker
    modCompileOnly "curse.maven:deeperdarker-659011:5906086"

    // Farmer's Delight https://www.curseforge.com/minecraft/mc-mods/farmers-delight
    modCompileOnly "curse.maven:farmers-delight-398521:6917255"

    // Hybrid Aquatic https://www.curseforge.com/minecraft/mc-mods/hybrid-aquatic
    modCompileOnly "curse.maven:hybrid-aquatic-834427:6497304"

    // Immersive Engineering https://www.curseforge.com/minecraft/mc-mods/immersive-engineering
    modCompileOnly "curse.maven:immersive-engineering-231951:6206989"

    // Moonlight https://www.curseforge.com/minecraft/mc-mods/selene
    modCompileOnly "curse.maven:selene-499980:6983934"

    // Repurposed Structures https://www.curseforge.com/minecraft/mc-mods/repurposed-structures
    modCompileOnly "curse.maven:repurposed-structures-368293:6797349"

    // Sawmill https://www.curseforge.com/minecraft/mc-mods/sawmill
    modCompileOnly "curse.maven:sawmill-964817:6948522"

    // Snow! Real Magic! https://www.curseforge.com/minecraft/mc-mods/snow-real-magic
    modCompileOnly "curse.maven:snow-real-magic-308663:6888541"

    // Supplementaries https://www.curseforge.com/minecraft/mc-mods/supplementaries
    modCompileOnly "curse.maven:supplementaries-412082:6974860"

    // The Bumblezone https://www.curseforge.com/minecraft/mc-mods/the-bumblezone-forge
    modCompileOnly "curse.maven:the-bumblezone-forge-362479:6960926"

    // Villagers plus https://www.curseforge.com/minecraft/mc-mods/villagersplus-forge
    modCompileOnly "curse.maven:villagersplus-forge-817272:4996995"

    // Mantle https://www.curseforge.com/minecraft/mc-mods/mantle
    modCompileOnly "curse.maven:mantle-74924:6819217"
}

processResources {
    from project(":common-emi").sourceSets.main.resources
    from project(":common-lootjs").sourceSets.main.resources
}

shadowJar {
    exclude "fabric.mod.json"
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}