plugins {
    id "architectury-plugin" version "3.4-SNAPSHOT"
    id "dev.architectury.loom" version "1.10-SNAPSHOT" apply false
    id "com.gradleup.shadow" version "8.3.6" apply false
}

architectury {
    minecraft = minecraft_version
}

def platforms = enabled_platforms.split(",")
def viewers = ["emi", "jei", "rei"]

subprojects {
    apply plugin: "dev.architectury.loom"

    loom {
        silentMojangMappingsLicense()
    }

    dependencies {
        implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'
        minecraft "com.mojang:minecraft:${minecraft_version}"
        mappings loom.officialMojangMappings()
    }

    if (platforms.contains(project.name)) {
        def emi_available = emi_enabled == "true" && project.findProperty("${project.name}_emi_enabled") == "true"
        def jei_available = jei_enabled == "true" && project.findProperty("${project.name}_jei_enabled") == "true"
        def rei_available = rei_enabled == "true" && project.findProperty("${project.name}_rei_enabled") == "true"
        def lootjs_available = lootjs_enabled == "true" && project.findProperty("${project.name}_lootjs_enabled") == "true"

        project.ext.emi_available = emi_available
        project.ext.commonProjects = [":api", ":common"]

        if (emi_available) project.ext.commonProjects << ":common-emi"
        if (jei_available) project.ext.commonProjects << ":common-jei"
        if (rei_available) project.ext.commonProjects << ":common-rei"
        if (lootjs_available) project.ext.commonProjects << ":common-lootjs"

        // register tasks that allow running specific combination of recipe viewer and launcher
        viewers.each { viewer ->
            if (project.findProperty("${viewer}_enabled") == "true" && project.findProperty("${project.name}_${viewer}_enabled") == "true") {
                tasks.register("run${project.name.capitalize()}${viewer.capitalize()}Client", GradleBuild) {
                    group = "launchers"
                    description = "Launch ${project.name.toUpperCase()} with ${viewer.toUpperCase()} recipe viewer"
                    setTasks([":${project.name}:runClient".toString()])
                    startParameter.projectProperties = ["recipe_viewer": viewer]
                }
            }
        }

        dependencies {
            def recipe_viewer = project.findProperty("recipe_viewer")
            if (!recipe_viewer) {
                if (emi_available) recipe_viewer = "emi"
                else if (jei_available) recipe_viewer = "jei"
                else if (rei_available) recipe_viewer = "rei"
            }

            if (emi_available && recipe_viewer == "emi") {
                modRuntimeOnly "dev.emi:emi-${project.name}:${emi_version}"
            }
            if (jei_available && recipe_viewer == "jei") {
                modRuntimeOnly "mezz.jei:jei-${minecraft_version}-${project.name}:${jei_version}"
            }
            if (rei_available) {
                modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-api-${project.name}:${rei_version}"

                if (recipe_viewer == "rei") {
                    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-${project.name}:${rei_version}"

                    if (project.name == "forge") {
                        modRuntimeOnly "me.shedaniel.cloth:cloth-config-forge:${cloth_config_version}"
                        modRuntimeOnly "dev.architectury:architectury-forge:${architectury_version}"
                    }
                }
            }
        }

        processResources {
            from project(":common").sourceSets.main.resources

            if (emi_available) {
                from project(":common-emi").sourceSets.main.resources
            }
            if (lootjs_available) {
                from project(":common-lootjs").sourceSets.main.resources
            }

            inputs.property "version", project.version

            def expandProps = [
                    "version"                   : project.version,
                    "group"                     : project.group,

                    "mod_name"                  : mod_name,
                    "mod_author"                : mod_author,
                    "mod_id"                    : mod_id,
                    "license"                   : license,
                    "description"               : project.description,

                    "minecraft_version"         : minecraft_version,
                    "minecraft_version_range"   : minecraft_version_range,

                    "forge_version"             : forge_version,
                    "forge_loader_version_range": forge_loader_version_range,
                    "forge_version_range"       : forge_version_range,

                    "fabric_version"            : fabric_version,
                    "fabric_loader_version"     : fabric_loader_version,

                    "emi_version"               : emi_version,
                    "emi_version_range"         : emi_version_range,
                    "emi_version_range_fabric"  : emi_version_range_fabric,

                    "jei_version"               : jei_version,
                    "jei_version_range"         : jei_version_range,
                    "jei_version_range_fabric"  : jei_version_range_fabric,

                    "rei_version"               : rei_version,
                    "rei_version_range"         : rei_version_range,
                    "rei_version_range_fabric"  : rei_version_range_fabric,
                    "cloth_config_version"      : cloth_config_version,
                    "architectury_version"      : architectury_version
            ]

            filesMatching(["fabric.mod.json", "META-INF/mods.toml", "pack.mcmeta"]) {
                expand expandProps
            }
        }

        sourceSets {
            main {
                resources {
                    srcDir "src/main/generated"
                    exclude ".cache/**"
                }
            }
        }
    }
}

allprojects {
    apply plugin: "java"
    apply plugin: "architectury-plugin"

    base {
        archivesName = mod_name
    }

    version = minecraft_version + "-" + project.version
    group = group

    repositories {
        maven { // EMI
            name = "EMI"
            url = "https://maven.terraformersmc.com/"
        }
        maven {
            name = "JEI"
            url "https://maven.blamejared.com"
        }
        maven {
            name = "REI"
            url "https://maven.shedaniel.me"
        }
        maven {
            url "https://www.cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven { // KubeJS
            // Shedaniel's maven (Architectury API)
            url = "https://maven.architectury.dev"
            content {
                includeGroup "dev.architectury"
            }
        }
        maven { // KubeJS
            // saps.dev Maven (KubeJS and Rhino)
            url = "https://maven.saps.dev/minecraft"
            content {
                includeGroup "dev.latvian.mods"
            }
        }
        maven { // CurseForge
            url "https://www.cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
    }

    tasks.withType(JavaCompile).configureEach {
        options.encoding = "UTF-8"
        options.release.set(17)
    }

    java {
        toolchain.languageVersion = JavaLanguageVersion.of(17)
        withSourcesJar()
    }
}
