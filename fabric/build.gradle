plugins {
    id 'com.gradleup.shadow'
}

base {
    archivesName = "${mod_name}-fabric"
}

architectury {
    platformSetupLoomIde()
    fabric()
}

loom {
    accessWidenerPath = project(":common").loom.accessWidenerPath

    mixin {
        defaultRefmapName = "ali.fabric.refmap.json"
    }

    runs {
        datagen {
            inherit client
            name "Minecraft Data"
            vmArg "-Dfabric-api.datagen"
            vmArg "-Dfabric-api.datagen.output-dir=${project(":fabric").file("src/main/generated")}"
            vmArg "-Dfabric-api.datagen.modid=${mod_id}"

            runDir "build/datagen"
        }
    }
}

configurations {
    common
    shadowCommon // Don't use shadow from the shadow plugin since it *excludes* files.
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentFabric.extendsFrom common
}

dependencies {
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modApi "net.fabricmc.fabric-api:fabric-api:${fabric_version}"

    project.ext.commonProjects.each { p ->
        common(project(path: p, configuration: "namedElements")) { transitive false }
        shadowCommon(project(path: p, configuration: "transformProductionFabric")) { transitive false }
    }

    // LootJS
//    modCompileOnly "curse.maven:kubejs-238086:5853321"
//    modCompileOnly "curse.maven:rhino-416294:6186969"
//    modCompileOnly "curse.maven:architectury-api-419699:5137936"
//    modCompileOnly "curse.maven:lootjs-570630:5404564"

    // Aether https://www.curseforge.com/minecraft/mc-mods/aether
//    modCompileOnly "curse.maven:aether-255308:6800393"

    // Charm Forked https://www.curseforge.com/minecraft/mc-mods/charm-forked
//    modCompileOnly "curse.maven:charm-forked-1016482:6492694"

    // Cultural Delights https://www.curseforge.com/minecraft/mc-mods/cultural-delights-fabric
//    modCompileOnly "curse.maven:cultural-delights-fabric-807107:5510598"

    // Deeper and Darker https://www.curseforge.com/minecraft/mc-mods/deeperdarker
    modCompileOnly "curse.maven:deeperdarker-659011:5072875"

    // Farmer's Delight https://www.curseforge.com/minecraft/mc-mods/farmers-delight-refabricated
//    modCompileOnly "curse.maven:farmers-delight-refabricated-993166:6626608"

    // Hybrid Aquatic https://www.curseforge.com/minecraft/mc-mods/hybrid-aquatic
//    modCompileOnly "curse.maven:hybrid-aquatic-834427:6497302"

    // Moonlight https://www.curseforge.com/minecraft/mc-mods/selene
//    modCompileOnly "curse.maven:selene-499980:6978610"

    // Repurposed Structures https://www.curseforge.com/minecraft/mc-mods/repurposed-structures-fabric
//    modCompileOnly "curse.maven:repurposed-structures-fabric-391366:6797350"

    // Sawmill https://www.curseforge.com/minecraft/mc-mods/sawmill
//    modCompileOnly "curse.maven:sawmill-964817:6948520"

    // Snow! Real Magic! https://www.curseforge.com/minecraft/mc-mods/snow-real-magic-fabric
//    modCompileOnly "curse.maven:snow-real-magic-fabric-560441:6779484"

    // Supplementaries https://www.curseforge.com/minecraft/mc-mods/supplementaries
//    modCompileOnly "curse.maven:supplementaries-412082:6889002"

    // The Bumblezone https://www.curseforge.com/minecraft/mc-mods/the-bumblezone-fabric
//    modCompileOnly "curse.maven:the-bumblezone-fabric-363949:6960934"

    // Villagers plus https://www.curseforge.com/minecraft/mc-mods/villagersplus-fabric
//    modCompileOnly "curse.maven:villagersplus-fabric-809542:4996993"
}

shadowJar {
    exclude "architectury.common.json"

    configurations = [project.configurations.shadowCommon]
    archiveClassifier = "dev-shadow"
}

remapJar {
    injectAccessWidener = true
    input.set shadowJar.archiveFile
    dependsOn shadowJar
}

sourcesJar {
    def commonSources = project(":common").sourcesJar
    dependsOn commonSources
    from commonSources.archiveFile.map { zipTree(it) }
}

components.java {
    withVariantsFromConfiguration(project.configurations.shadowRuntimeElements) {
        skip()
    }
}