plugins {
    id 'java'
    id 'idea'
    id 'fabric-loom' version '1.11-SNAPSHOT'
}

base {
    archivesName = "${mod_name}-fabric-${minecraft_version}"
}

fabricApi {
    configureDataGeneration {
        modId = "ali"
    }
}

dependencies {
    minecraft "com.mojang:minecraft:${minecraft_version}"
    mappings loom.officialMojangMappings()
    modImplementation "net.fabricmc:fabric-loader:${fabric_loader_version}"
    modImplementation "net.fabricmc.fabric-api:fabric-api:${fabric_version}"
    implementation group: 'com.google.code.findbugs', name: 'jsr305', version: '3.0.1'

    implementation project(":api")
    implementation project(":common")

    // EMI
    modCompileOnlyApi "dev.emi:emi-fabric:${emi_version}:api"
    modCompileOnly "dev.emi:emi-fabric:${emi_version}"
//    modLocalRuntime "dev.emi:emi-fabric:${emi_version}"

    // JEI
    modCompileOnlyApi("mezz.jei:jei-${minecraft_version}-fabric-api:${jei_version}")
    modRuntimeOnly("mezz.jei:jei-${minecraft_version}-fabric:${jei_version}")

    // REI
    modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-api-fabric:${rei_version}"
    modCompileOnlyApi "me.shedaniel:RoughlyEnoughItems-default-plugin-fabric:${rei_version}"
//    modRuntimeOnly "me.shedaniel:RoughlyEnoughItems-fabric:${rei_version}"
    modCompileOnlyApi "me.shedaniel.cloth:cloth-config-fabric:${cloth_config_version}"
    modCompileOnlyApi "dev.architectury:architectury-fabric:${architectury_version}"

    // LootJS
//    modImplementation "curse.maven:kubejs-238086:5853321"
//    modImplementation "curse.maven:rhino-416294:6186969"
//    modImplementation "curse.maven:architectury-api-419699:5137936"
//    modImplementation "curse.maven:lootjs-570630:5404564"

    // Aether https://www.curseforge.com/minecraft/mc-mods/aether
    modCompileOnly "curse.maven:aether-255308:6894657"

    // Charm Forked https://www.curseforge.com/minecraft/mc-mods/charm-forked
    modCompileOnly "curse.maven:charm-forked-1016482:6143797"

    // Cultural Delights https://www.curseforge.com/minecraft/mc-mods/cultural-delights-fabric
//    modCompileOnly "curse.maven:cultural-delights-fabric-807107:5510598"

    // Deeper and Darker https://www.curseforge.com/minecraft/mc-mods/deeperdarker
    modCompileOnly "curse.maven:deeperdarker-659011:6007579"

    // Farmer's Delight https://www.curseforge.com/minecraft/mc-mods/farmers-delight-refabricated
    modCompileOnly "curse.maven:farmers-delight-refabricated-993166:6955339"

    // Hybrid Aquatic https://www.curseforge.com/minecraft/mc-mods/hybrid-aquatic
//    modCompileOnly "curse.maven:hybrid-aquatic-834427:6497302"

    // Moonlight https://www.curseforge.com/minecraft/mc-mods/selene
    modCompileOnly "curse.maven:selene-499980:7007382"

    // Repurposed Structures https://www.curseforge.com/minecraft/mc-mods/repurposed-structures-fabric
    modCompileOnly "curse.maven:repurposed-structures-fabric-391366:6797358"

    // Sawmill https://www.curseforge.com/minecraft/mc-mods/sawmill
    modCompileOnly "curse.maven:sawmill-964817:6964417"

    // Snow! Real Magic! https://www.curseforge.com/minecraft/mc-mods/snow-real-magic-fabric
    modCompileOnly "curse.maven:snow-real-magic-fabric-560441:6783623"

    // Supplementaries https://www.curseforge.com/minecraft/mc-mods/supplementaries
    modCompileOnly "curse.maven:supplementaries-412082:7008518"

    // The Bumblezone https://www.curseforge.com/minecraft/mc-mods/the-bumblezone-fabric
    modCompileOnly "curse.maven:the-bumblezone-fabric-363949:6960934"

    // Villagers plus https://www.curseforge.com/minecraft/mc-mods/villagersplus-fabric
//    modCompileOnly "curse.maven:villagersplus-fabric-809542:4996993"
}

loom {
    if (project(":common").file("src/main/resources/${mod_id}.accesswidener").exists()) {
        accessWidenerPath.set(project(":common").file("src/main/resources/${mod_id}.accesswidener"))
    }

    mixin {
        defaultRefmapName.set("${mod_id}.refmap.json")
    }

    runs {
        client {
            client()
            setConfigName("Fabric Client")
            ideConfigGenerated(true)
            runDir("run")
        }

        server {
            server()
            setConfigName("Fabric Server")
            ideConfigGenerated(true)
            runDir("run")
        }
    }
}

tasks.named('compileTestJava') {
    it.enabled = false
}

tasks.named('test') {
    it.enabled = false
}

tasks.withType(JavaCompile).configureEach {
    source(project(":api").sourceSets.main.allSource)
    source(project(":common").sourceSets.main.allSource)
    source(project(":common-emi").sourceSets.main.allSource)
    source(project(":common-jei").sourceSets.main.allSource)
    source(project(":common-rei").sourceSets.main.allSource)
//    source(project(":common-lootjs").sourceSets.main.allSource)
}

tasks.named("sourcesJar", Jar) {
    from(project(":api").sourceSets.main.allSource)
    from(project(":common").sourceSets.main.allSource)
    from(project(":common-emi").sourceSets.main.allSource)
    from(project(":common-jei").sourceSets.main.allSource)
    from(project(":common-rei").sourceSets.main.allSource)
//    from(project(":common-lootjs").sourceSets.main.allSource)
}

processResources {
    from project(":api").sourceSets.main.resources
    from project(":common").sourceSets.main.resources
    from project(":common-emi").sourceSets.main.resources
    from project(":common-jei").sourceSets.main.resources
//    from project(":common-lootjs").sourceSets.main.resources
}